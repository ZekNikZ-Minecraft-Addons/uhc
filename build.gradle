plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id "io.papermc.paperweight.userdev" version "1.5.10"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "org.jlleitschuh.gradle.ktlint" version "11.6.1"
    id 'maven-publish'
}

group = 'io.zkz.mc'
version = '3.0.0-SNAPSHOT'

ext {
    // SDK
    kotlinVersion = '1.9.20'
    targetJavaVersion = 17

    // Libraries
    paperVersion = '1.20.4-R0.1-SNAPSHOT'

    // Plugins
    gametoolsVersion = '6.0.0-SNAPSHOT'
    minigameManagerVersion = '3.0.0-SNAPSHOT'

    // MC
    apiVersion = '1.20'
}

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven {
        url = "https://maven.pkg.github.com/ZekNikZ-Minecraft-Addons/gametools"
        credentials {
            username = project.findProperty("gpr.username") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.access_token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url = "https://maven.pkg.github.com/ZekNikZ-Minecraft-Addons/minigamemanager"
        credentials {
            username = project.findProperty("gpr.username") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.access_token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:${paperVersion}")

    compileOnly "io.zkz.mc:gametools:${gametoolsVersion}"
    compileOnly "io.zkz.mc:minigamemanager:${minigameManagerVersion}"
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion as String)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = targetJavaVersion
}

processResources {
    def props = [
            version   : version,
            name      : project.name,
            apiVersion: apiVersion
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(targetJavaVersion)
}

publishing {
    publications {
        libraryJar(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ZekNikZ-Minecraft-Addons/uhc"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.assemble {
    dependsOn(reobfJar)
}

// Uncomment this to force SNAPSHOTs to update
//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}
